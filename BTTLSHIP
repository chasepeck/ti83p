ClrHome
Output(3,6,"TI-83+")
Output(4,4,"BATTLESHIP")
Output(6,4,"CHASE PECK")
Pause
{8,8}→dim([A])
{8,16}→dim([B])
{1,1,1}→ʟA
{1,1,1}→ʟB
Fill(0,[A])
0→T
8→C
Lbl TU
ClrHome
Output(3,2,"PLAYER  :")
Output(3,9,T+1)
Output(4,2,"PLACE SHIPS")
Output(6,2,"ARROWS+ENTER")
Output(7,2,"2ND TO ROTATE")
Pause
ClrHome
4→S
0→R
1→Z
1→Θ
Lbl LD
0→K
Fill(1,[B])
For(Y,1,4)
If [A](Y+4*T,1)>0:Then
[A](Y+4*T,3)→H
[A](Y+4*T,4)→W
For(X,1,max(W,H))
[A](Y+4*T,1)+min(H-1,X-1)→A
[A](Y+4*T,2)+min(W-1,X-1)→B
Output(A,B,sub("*X",[A](Y+4*T,4+X)+1,1))
max([A](Y+4*T,4+X)+2,[B](A,B))→[B](A,B)
End
End
End
Lbl CU
While 1
If C>0:Then
Output(8,1,S)
If R=0:Then
Output(8,2," DOWN ")
Else
Output(8,2," RIGHT")
End
End
Output(Θ,Z,"?")
While K=0
getKey→K
If K=105:Then
Output(8,1,"...            ")
If C≤0:Goto SH
Goto PL
End
End
Output(Θ,Z,sub(" *XO",[B](Θ,Z),1))
If K=24
max(Z-1,1)→Z
If K=25
max(Θ-1,1)→Θ
If K=26
min(Z+1,16-(S-1)*R)→Z
If K=34
min(Θ+1,8-(S-1)*abs(R-1))→Θ
If K=21
abs(R-1)→R
If K=22
Goto Z
getKey→K
End
Lbl PL
Θ→[A](9-C,1)
Z→[A](9-C,2)
If R=0:Then
S→[A](9-C,3)
1→[A](9-C,4)
Else
1→[A](9-C,3)
S→[A](9-C,4)
End
C-1→C
max(abs(C-4),2)→S
If C=4:Then
1→T
Goto TU
End
If C≤0:Then
1→S
Lbl T2
ClrHome
abs(T-1)→T
Output(3,2,"PLAYER  :")
Output(3,9,T+1)
Output(4,2,"BLAST ASS!")
Output(6,2,"ARROWS+ENTER")
Pause
ClrHome
1→Z
1→Θ
Fill(1,[B])
If T=0:Then
For(X,1,dim(ʟA),3)
Output(ʟA(X),ʟA(X+1),sub("  XO",ʟA(X+2),1)
ʟA(X+2)→[B](ʟA(X),ʟA(X+1))
End
Else
For(X,1,dim(ʟB),3)
Output(ʟB(X),ʟB(X+1),sub("  XO",ʟB(X+2),1)
ʟB(X+2)→[B](ʟB(X),ʟB(X+1))
End
End
Goto CU
End
Goto LD
Lbl SH
4→A
0→B
For(Y,1,4)
[A](Y+4*abs(T-1),3)→H
[A](Y+4*abs(T-1),4)→W
For(X,1,max(W,H))
If [A](Y+4*abs(T-1),1)+min(H-1,X-1)=Θ and [A](Y+4*abs(T-1),2)+min(W-1,X-1)=Z:Then
1→[A](Y+4*abs(T-1),X+4)
Output(8,1,"HIT!")
Output(Θ,Z,"X")
If [A](Y+4*abs(T-1),5)+[A](Y+4*abs(T-1),6)+[A](Y+4*abs(T-1),7)+[A](Y+4*abs(T-1),8)=max(W,H)
Output(8,6,"SUNK!")
3→A
End
B+[A](Y+4*abs(T-1),X+4)→B
End
End
If B≥11:Goto WI
If T=0:Then
dim(ʟA)+3→dim(ʟA)
Θ→ʟA(dim(ʟA)-2)
Z→ʟA(dim(ʟA))
Else
dim(ʟB)+3→dim(ʟB)
Θ→ʟB(dim(ʟB)-2)
Z→ʟB(dim(ʟB)-1)
A→ʟB(dim(ʟB))
End
If A=4:Then
Output(8,1,"MISS.")
Output(Θ,Z,"O")
End
Pause
Goto T2
Lbl WI
ClrHome
Output(4,2,"PLAYER")
Output(4,9,T+1)
Output(5,2,"EMERGES")
Output(6,2,"VICTORIOUS!")
Pause
Lbl Z
DelVar A
DelVar B
DelVar C
DelVar D
DelVar H
DelVar K
DelVar R
DelVar S
DelVar T
DelVar W
DelVar X
DelVar Z
DelVar Θ
DelVar ʟA
DelVar ʟB
DelVar [A]
DelVar [B]
